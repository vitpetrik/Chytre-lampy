#include <Arduino.h>
#include <Wire.h>
#include <WiFi.h>
#include <WiFiClient.h>
#include <math.h>
#include <ESPmDNS.h>
#include <WiFiUdp.h>
#include <ArduinoOTA.h>

// FreeRTOS Semaphore pro zamezeni konfliktu p콏i p콏istupov치n칤 triggrovac칤ch prom캩nn칳ch
SemaphoreHandle_t trigger_mutex = xSemaphoreCreateMutex();
SemaphoreHandle_t lamp_mutex = xSemaphoreCreateMutex();
SemaphoreHandle_t telnetWrite_mutex = xSemaphoreCreateMutex();
SemaphoreHandle_t telnetRead_mutex = xSemaphoreCreateMutex();

// init globalnich promennych
uint8_t triggerPos[2] = {0, 0};
uint8_t triggerCount = 0;
unsigned long triggerNum = 0;

String telnetMessage = "";
uint8_t telnetCount = 0;
unsigned long telnetNum = 0;

uint8_t lampCount = 0;

uint8_t low = 5;
uint8_t high = 255;
uint16_t interval = 1000;
uint8_t radius = 25;

#include <ota.h>
#include <telnet.h>
#include <lamp.h>

//Task pro o코칠fen칤 polom캩ru
void lampTrigger(void *parameters)
{
  //inicializace
  uint8_t address = int(parameters);

  uint8_t pos[2] = {0, 0};
  uint8_t *p = readPosition(address);
  pos[0] = p[0];
  pos[1] = p[1];

  unsigned long onMillis = 0;
  unsigned long lastTrigger = 0;
  bool on = false;
  double rad;

  while (true)
  {
    if (xSemaphoreTake(trigger_mutex, 20) == pdTRUE) //po쮂멳치 o semafor
    {
      if (triggerNum != lastTrigger) //pokud je zaznamen치n nov칳 trigger
      {
        rad = sqrt(pow(pos[0] - triggerPos[0], 2) + pow(pos[1] - triggerPos[1], 2)); //v칳po캜et polom캩ru
        lastTrigger = triggerNum;
        triggerCount--; //dekrementace

        if (triggerCount == 0) //pokud trigger zpracovali v코echny tasky uvoln칤me semafor pro 캜ten칤 lamp
        {
          xSemaphoreGive(lamp_mutex);
        }
        xSemaphoreGive(trigger_mutex); //uvoln칤me semafor pro polom캩r

        if (rad <= radius) //pokud jsme v polom캩ru
        {
          if (!on) //pokud je lampa zhasnut치
          {
            on = true;
            writePWM(address, high); //zapneme lampu
            writeStringTelnetln("Z-X-" + String(pos[0], HEX) + "-Y-" + String(pos[1], HEX));
          }
          onMillis = millis(); //nastav칤me 캜as pro v칳po캜et intervalu
        }
      }
      else
      {
        xSemaphoreGive(trigger_mutex); //vr치t칤me semafor pro polom캩r
      }
    }

    //pokud je lampa rozsv칤cena a z치rove켿 jsme mimo interval
    if (on && (millis() - onMillis) > interval)
    {
      on = false;
      writePWM(address, low); //vypneme lampu
      writeStringTelnetln("V-X-" + String(pos[0], HEX) + "-Y-" + String(pos[1], HEX));
    }
    taskYIELD();
  }
}

// task kazde lampy
void lamp(void *parameters)
{
  //inicializace
  uint8_t address = int(parameters);
  bool discoMode = false;
  unsigned long lastTelnetNum = 0;
  String foo = "";

  uint8_t pos[2] = {0, 0};
  uint8_t *p = readPosition(address);
  pos[0] = p[0];
  pos[1] = p[1];

  // smycka tasku lampy 游눠
  while (true)
  {
    if (xSemaphoreTake(telnetRead_mutex, 20))
    {
      if (lastTelnetNum != telnetNum)
      {
        foo = telnetMessage;
        lastTelnetNum = telnetNum;
        telnetCount--;

        if (telnetCount == 0)
        {
          xSemaphoreGive(telnetWrite_mutex);
        }
        xSemaphoreGive(telnetRead_mutex);

        if (foo.indexOf("disco") >= 0)
        {
          discoMode = !discoMode;
        }
      }
      else
      {
        xSemaphoreGive(telnetRead_mutex);
      }
    }
    //disco m칩d :)
    if (!discoMode)
    {
      if (xSemaphoreTake(lamp_mutex, 20) == pdTRUE) //po쮂멳치me o semafor pro 캜ten칤 lamp
      {
        if (readTouch(address) == 1) //pokud m치me dotyk
        {
          //zap칤코eme sou콏adnice triggeru
          triggerPos[0] = pos[0];
          triggerPos[1] = pos[1];
          triggerCount = lampCount;
          triggerNum++;
          writeStringTelnetln("T-X-" + String(pos[0], HEX) + "-Y-" + String(pos[1], HEX));
          delay(50); //tato delay zde nemus칤 nutn캩 b칳t, ale malinko odleh캜칤 sb캩rnici :)
        }
        else
        {
          //pokud nen칤 dotyk vr치t칤me semafor, jinak ho nevrac칤me!!!
          xSemaphoreGive(lamp_mutex);
        }
      }
    }
    else
    {
      disco(address); //游붃游붃游붃游붃游붃游붃
    }
    taskYIELD();
  }
  vTaskDelete(NULL);
}

//nastaven칤 lampy
void lampInit(void *parameters)
{
  uint8_t address = int(parameters);

  uint8_t pos[2] = {0, 0};
  uint8_t *p = readPosition(address);
  pos[0] = p[0];
  pos[1] = p[1];

  //odeslani informace o poloze lampy pri jejim nalezeni na Telnet
  writeStringTelnetln("L-X-" + String(pos[0], HEX) + "-Y-" + String(pos[1], HEX));

  //inkrementace po캜tu lamp
  while (true)
  {
    if (xSemaphoreTake(trigger_mutex, 20) == pdTRUE)
    {
      lampCount++;
      xSemaphoreGive(trigger_mutex);
      break;
    }
  }

  //nastaven칤 m칩du lampy
  writeMode(address, 1);

  //3x zablik치n칤 lampy
  for (int i = 0; i < 3; i++)
  {
    writePWM(address, high);
    delay(1000);
    writePWM(address, low);
    delay(1000);
  }
  writePWM(address, low);

  //vytvo콏en칤 task콢 nutn칳ch pro spr치vn칠 pracov치n칤
  xTaskCreatePinnedToCore(lamp, "lamp", 1500, (void *)address, 3, NULL, 1);
  xTaskCreatePinnedToCore(lampTrigger, "lampTrigger", 1500, (void *)address, 3, NULL, 1);

  vTaskDelete(NULL);
}

// vyhledani lampy na sbernici
void scanner(void *parameters)
{
  for (int i = 4; i < 50; i++)
  {
    if (isLampHere(i))
    {
      //pokud jsme na코li lampu vytvo콏칤me pro ni task
      xTaskCreatePinnedToCore(lampInit, "lamp", 1000, (void *)i, 5, NULL, 1);
    }
    delay(1);
  }
  vTaskDelete(NULL);
}

void setup()
{
  //inicializace ESP
  delay(500); //"bezpe캜nostn칤" zpo쬯캩n칤춷

  Serial.begin(115200);
  Wire.begin(22, 23);
  pinMode(22, INPUT);
  pinMode(23, INPUT);

  WiFi.softAP("ChytreLampy", "");

  MDNS.begin("chytrelampy");
  MDNS.addService("http", "tcp", 80);

  xTaskCreatePinnedToCore(serverHandle, "server", 2000, (void *)1, 3, NULL, 1);
  xTaskCreatePinnedToCore(OTA, "OTA", 2000, (void *)1, 3, NULL, 1);
  xTaskCreatePinnedToCore(scanner, "scanner", 2000, (void *)1, 5, NULL, 1);
  vTaskDelete(NULL);
}

void loop()
{
  vTaskDelete(NULL);
}